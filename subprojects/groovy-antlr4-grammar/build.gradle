if (!rootProject.hasProperty('useAntlr4')) return

apply plugin: 'me.champeau.gradle.antlr4'

def srcBase = 'subprojects/groovy-antlr4-grammar/src/'
def srcMain = srcBase + 'main/'
def srcTest = srcBase + 'test/'
// set package for antlr generated classes
antlr4 {
    source = file(srcMain+'antlr4')
    extraArgs = ["-package", "org.codehaus.groovy.parser.antlr4"]
    output =  file("$buildDir/generated-sources/org/codehaus/groovy/parser/antlr4")
}

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4

// add antlr4 to classpath
configurations {
   compile.extendsFrom antlr4
}

dependencies {
	testCompile project(':groovy-test')
    testCompile("org.spockframework:spock-core:$spockVersion") {
        exclude module: 'groovy-all'
    }
}

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += antlr4.output
sourceSets.main.java.srcDirs += file(srcMain+'java')
sourceSets.main.groovy.srcDirs += file(srcMain+'groovy')
sourceSets.test.groovy.srcDirs += file(srcTest+'groovy')
sourceSets.test.resources.srcDirs += file(srcTest+'resources')

allprojects {
   tasks.withType(GroovyCompile) {
	    // Enabling this would make the ANTLR4 grammar the default for the build itself. We're not quite there yet.
	    // groovyOptions.forkOptions.jvmArgs += ["-Dgroovy.antlr4=true"]
    }
}